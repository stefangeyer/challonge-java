buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group 'at.stefangeyer.challonge'
    version '2.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    dependencies {
        testCompile group: 'junit', name: 'junit', version: property('junit.version')
        testCompile group: 'org.mockito', name: 'mockito-core', version: property('mockito.version')
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    publishing {
        repositories {
            maven {
                credentials {
                    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                        username project.ossrhUsername
                        password project.ossrhPassword
                    }
                }

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }

        publications {
            mavenJava(MavenPublication) {
                afterEvaluate {
                    artifactId = jar.baseName
                }

                artifact sourcesJar
                artifact javadocJar

                from components.java

                pom {
                    url = 'https://github.com/stefangeyer/challonge-java'

                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://choosealicense.com/licenses/mit/'
                        }
                    }

                    developers {
                        developer {
                            id = 'sgeyer'
                            name = 'Stefan Geyer'
                            email = 'stefangeyer@outlook.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/stefangeyer/challonge-java.git'
                        developerConnection = 'scm:git:ssh://github.com/stefangeyer/challonge-java.git'
                        url = 'https://github.com/stefangeyer/challonge-java'
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}

task localPublish {
    dependsOn('core:localPublish')
    dependsOn('serializers:localPublish')
    dependsOn('rest:localPublish')
}

task remotePublish {
    dependsOn('core:remotePublish')
    dependsOn('serializers:remotePublish')
    dependsOn('rest:remotePublish')
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}